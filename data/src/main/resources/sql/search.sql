DROP FUNCTION RUS_TO_ENG;
//

DROP FUNCTION ENG_TO_RUS;
//

DROP TABLE SYMBOLS;
//

DROP FUNCTION FIND_TOUR_INFO;
//

DROP FUNCTION SEARCH;
//

DROP FUNCTION SEARCH_SIMPLE;
//

DROP FUNCTION SEARCH_LEVENSTEIN;
//

DROP TYPE LIST_TOUR_INFO;
//

CREATE OR REPLACE TYPE TOUR_INFO AS OBJECT (
	NAME VARCHAR2(100),
	NAME_PLACE VARCHAR2(50),
	TRAVEL_INFO VARCHAR2(32767),
	FLIGHT_INFO VARCHAR2(32767),
	HOTEL_INFO VARCHAR2(32767)
);
//

CREATE OR REPLACE TYPE LIST_TOUR_INFO IS TABLE OF TOUR_INFO;
//

CREATE TABLE SYMBOLS (
  ENG CHAR NOT NULL,
  RUS CHAR NOT NULL,
  
  CONSTRAINT SYMBOLS_PK PRIMARY KEY ( ENG, RUS )
);
//

CREATE OR REPLACE 
FUNCTION RUS_TO_ENG(S_IN IN VARCHAR2) 
RETURN VARCHAR2 IS
  S_OUT VARCHAR2(32767);
BEGIN
  S_OUT := UPPER(S_IN);
  FOR SYM IN (SELECT * FROM SYMBOLS) LOOP
    S_OUT := REPLACE(S_OUT, SYM.RUS, SYM.ENG);
  END LOOP;
  
  RETURN S_OUT;
END RUS_TO_ENG;
//

CREATE OR REPLACE 
FUNCTION ENG_TO_RUS(S_IN IN VARCHAR2)
RETURN VARCHAR2 IS
  S_OUT VARCHAR2(32767);
BEGIN
  S_OUT := UPPER(S_IN);
  FOR SYM IN (SELECT * FROM SYMBOLS) LOOP
    S_OUT := REPLACE(S_OUT, SYM.ENG, SYM.RUS);
  END LOOP;
  
  RETURN S_OUT;
END ENG_TO_RUS;
//

create or replace FUNCTION GET_PARTS(STR VARCHAR2)
RETURN T_PARTS
IS 
  RES T_PARTS := T_PARTS();
BEGIN
  FOR I IN 1 .. LENGTH(STR) - 2 LOOP
    RES.EXTEND;
    RES(I) := SUBSTR(STR, I, 3);
  END LOOP;
  RETURN RES;
END;
//

CREATE OR REPLACE FUNCTION FIND_TOUR_INFO
RETURN LIST_TOUR_INFO PIPELINED IS
BEGIN
	FOR I IN (
		SELECT 
  			TOUR.NAME TOUR_NAME,
  			PLACE.NAME PLACE_NAME,
  			LISTAGG('[' || TRAVEL.NUMBER_CHILD || ';' || TRAVEL.NUMBER_ADULTS || ']', ',') 
    			WITHIN GROUP (ORDER BY TRAVEL.ID) TRAVEL_INFO,
  			LISTAGG('[' || FLIGHT.TYPE_TRANSPORT || ' from ' || FLIGHT.LEAVING_DATE || ' to ' || FLIGHT.ARRIVAL_DATE || ']', ',') 
    			WITHIN GROUP (ORDER BY FLIGHT.ID) FLIGHT_INFO,
  			LISTAGG('[' || HOTEL.RATING_HOTEL || ' from ' || HOTEL.ARRIVAL_DATE || ' to ' || HOTEL.LEAVING_DATE || ']', ',') 
    			WITHIN GROUP (ORDER BY HOTEL.ID) HOTEL_INFO
		FROM TOURS TOUR
		JOIN PLACES PLACE ON PLACE.ID = TOUR.ID_PLACE
		JOIN TRAVELS TRAVEL ON TOUR.ID = TRAVEL.ID_TOUR
		JOIN FLIGHTS FLIGHT ON TOUR.ID = FLIGHT.ID_TOUR
		JOIN HOTELS HOTEL ON TOUR.ID = HOTEL.ID_TOUR
		GROUP BY TOUR.NAME, PLACE.NAME) LOOP
		PIPE ROW (TOUR_INFO(I.TOUR_NAME, I.PLACE_NAME, I.TRAVEL_INFO, I.FLIGHT_INFO, I.HOTEL_INFO));
	END LOOP;
END;
//

CREATE OR REPLACE 
FUNCTION SEARCH_SIMPLE(STR IN VARCHAR2)
RETURN LIST_TOUR_INFO PIPELINED
IS
  S_IN VARCHAR2(32767) := '%' || TRIM(STR) || '%';
  S_RUS VARCHAR2(32767) := '%' || ENG_TO_RUS(TRIM(STR)) || '%';
  S_ENG VARCHAR2(32767) := '%' || RUS_TO_ENG(trim(STR)) || '%';
BEGIN
  IF LENGTH(TRIM(STR)) IS NULL THEN
    RETURN;
  END IF;
  FOR I IN (
      SELECT *
      FROM TABLE(FIND_TOUR_INFO()) 
      WHERE 
        UPPER(NAME) LIKE S_IN OR UPPER(NAME) LIKE S_RUS OR UPPER(NAME) LIKE S_ENG OR 
        UPPER(NAME_PLACE) LIKE S_IN OR UPPER(NAME_PLACE) LIKE S_RUS OR UPPER(NAME_PLACE) LIKE S_ENG OR
        UPPER(TRAVEL_INFO) LIKE S_IN OR UPPER(TRAVEL_INFO) LIKE S_RUS OR UPPER(TRAVEL_INFO) LIKE S_ENG OR 
        UPPER(FLIGHT_INFO) LIKE S_IN OR UPPER(FLIGHT_INFO) LIKE S_RUS OR UPPER(FLIGHT_INFO) LIKE S_ENG OR 
        UPPER(HOTEL_INFO) LIKE S_IN OR UPPER(HOTEL_INFO) LIKE S_RUS OR UPPER(HOTEL_INFO) LIKE S_ENG) LOOP
    PIPE ROW (TOUR_INFO(I.NAME, I.NAME_PLACE, I.TRAVEL_INFO, I.FLIGHT_INFO, I.HOTEL_INFO));
  END LOOP;
END;
//

CREATE OR REPLACE FUNCTION SEARCH_LEVENSTEIN(STR VARCHAR2)
RETURN LIST_TOUR_INFO PIPELINED
IS
  S_IN VARCHAR(32767) := TRIM(STR);
  S_RUS VARCHAR2(32767) := ENG_TO_RUS(S_IN);
  S_ENG VARCHAR2(32767) := RUS_TO_ENG(S_IN);
BEGIN
  FOR I IN (
      SELECT *
      FROM TABLE(FIND_TOUR_INFO()) 
      WHERE 
          UTL_MATCH.EDIT_DISTANCE_SIMILARITY(S_IN, UPPER(NAME)) >= 50 OR
          UTL_MATCH.EDIT_DISTANCE_SIMILARITY(S_RUS, UPPER(NAME)) >= 50 OR
          UTL_MATCH.EDIT_DISTANCE_SIMILARITY(S_ENG, UPPER(NAME)) >= 50 OR
          UTL_MATCH.EDIT_DISTANCE_SIMILARITY(S_IN, UPPER(NAME_PLACE)) >= 50 OR
          UTL_MATCH.EDIT_DISTANCE_SIMILARITY(S_RUS, UPPER(NAME_PLACE)) >= 50 OR
          UTL_MATCH.EDIT_DISTANCE_SIMILARITY(S_ENG, UPPER(NAME_PLACE)) >= 50) LOOP
    PIPE ROW (TOUR_INFO(I.NAME, I.NAME_PLACE, I.TRAVEL_INFO, I.FLIGHT_INFO, I.HOTEL_INFO));
  END LOOP;
END;
//

CREATE OR REPLACE FUNCTION SEARCH_JARO_WINKLER(STR VARCHAR2)
RETURN LIST_TOUR_INFO PIPELINED
IS
  S_IN VARCHAR(32767) := TRIM(STR);
  S_RUS VARCHAR2(32767) := ENG_TO_RUS(S_IN);
  S_ENG VARCHAR2(32767) := RUS_TO_ENG(S_IN);
BEGIN
  FOR I IN (
      SELECT *
      FROM TABLE(FIND_TOUR_INFO()) 
      WHERE 
        UTL_MATCH.JARO_WINKLER_SIMILARITY(S_IN, UPPER(NAME)) >= 50 OR
        UTL_MATCH.JARO_WINKLER_SIMILARITY(S_RUS, UPPER(NAME)) >= 50 OR
        UTL_MATCH.JARO_WINKLER_SIMILARITY(S_ENG, UPPER(NAME)) >= 50 OR
        UTL_MATCH.JARO_WINKLER_SIMILARITY(S_IN, UPPER(NAME_PLACE)) >= 50 OR
        UTL_MATCH.JARO_WINKLER_SIMILARITY(S_RUS, UPPER(NAME_PLACE)) >= 50 OR
        UTL_MATCH.JARO_WINKLER_SIMILARITY(S_ENG, UPPER(NAME_PLACE)) >= 50) LOOP
    PIPE ROW (TOUR_INFO(I.NAME, I.NAME_PLACE, I.TRAVEL_INFO, I.FLIGHT_INFO, I.HOTEL_INFO));
  END LOOP;
END;
//

create or replace FUNCTION SEARCH_NGRAM(STR VARCHAR2)
RETURN LIST_TOUR_INFO PIPELINED
IS
  Q T_PARTS := GET_PARTS(STR);
  Q_RUS T_PARTS := GET_PARTS(ENG_TO_RUS(STR));
  Q_ENG T_PARTS := GET_PARTS(RUS_TO_ENG(STR));
  CNT INTEGER := 0;
  UP_NAME VARCHAR2(100);
BEGIN
  FOR ITEM IN (SELECT * FROM TABLE (FIND_TOUR_INFO)) LOOP
    CNT := 0;
    UP_NAME := UPPER(ITEM.NAME);
    FOR I IN Q.FIRST .. Q.LAST LOOP
      IF INSTR(UP_NAME, Q(I)) <> 0 OR INSTR(UP_NAME, Q_RUS(I)) <> 0 OR INSTR(UP_NAME, Q_ENG(I)) <> 0 THEN
        CNT := CNT + 1;
      END IF;
    END LOOP;
    IF (CNT * 2 >= Q.COUNT) THEN
      PIPE ROW (TOUR_INFO(ITEM.NAME, ITEM.NAME_PLACE, ITEM.TRAVEL_INFO, ITEM.FLIGHT_INFO, ITEM.HOTEL_INFO));
    END IF;
  END LOOP;
END;
//

create or replace FUNCTION SEARCH(STR VARCHAR2, S_TYPE VARCHAR2)
RETURN LIST_TOUR_INFO PIPELINED
IS
  S_IN VARCHAR2(32767) := UPPER(STR);
BEGIN
  CASE UPPER(S_TYPE)
    WHEN 'SIMPLE' THEN 
      FOR I IN (SELECT * FROM TABLE (SEARCH_SIMPLE(S_IN))) LOOP
        PIPE ROW (TOUR_INFO(I.NAME, I.NAME_PLACE, I.TRAVEL_INFO, I.FLIGHT_INFO, I.HOTEL_INFO));
      END LOOP;
    WHEN 'LEVENSTEIN' THEN
      FOR I IN (SELECT * FROM TABLE (SEARCH_LEVENSTEIN(S_IN))) LOOP
        PIPE ROW (TOUR_INFO(I.NAME, I.NAME_PLACE, I.TRAVEL_INFO, I.FLIGHT_INFO, I.HOTEL_INFO));
      END LOOP;
    WHEN 'JARO_WINKLER' THEN
      FOR I IN (SELECT * FROM TABLE (SEARCH_JARO_WINKLER(S_IN))) LOOP
        PIPE ROW (TOUR_INFO(I.NAME, I.NAME_PLACE, I.TRAVEL_INFO, I.FLIGHT_INFO, I.HOTEL_INFO));
      END LOOP;
    WHEN 'NGRAM' THEN
      FOR I IN (SELECT * FROM TABLE (SEARCH_NGRAM(S_IN))) LOOP
        PIPE ROW (TOUR_INFO(I.NAME, I.NAME_PLACE, I.TRAVEL_INFO, I.FLIGHT_INFO, I.HOTEL_INFO));
      END LOOP;
  END CASE;
END;
//